# 项目07: 跟随我
![Img](/media/img-20230518083845.png)

## 1.实验简介：
在上面实验中，我们已经了解了8*8点阵、电机驱动和调速、超声波传感器、舵机等硬件知识，那么在本实验中，我们将结合它们打造一款跟随小车! 在电路设计过程中，我们可以利用超声波传感器来检测小车与前方物体的距离。通过测量距离控制电机的旋转，从而控制小车的运动状态，使小车跟随物体运动。
## 2.工作原理：
|检测|检测前方物体的距离（单位：cm）|
| :--: | :--: |
|条件1|距离＜8|
|状态|小车后退|
|条件2|8≤距离<13|
|状态|小车停止|
|条件3|13≤距离<35|
|状态|小车前进|
|条件3|距离≥35|
|状态|小车停止|

## 3.流程图：
![Img](/media/img-20230330120246.png)

## 4.实验代码：

```
//*************************************************************************************
/*
Project 07: 跟随我
Car follows the object
*/ 
//电机
#define left_ctrl  33  //定义左电机方向控制引脚gpio33.
#define left_pwm  26   //定义左电机PWM控制引脚gpio26.
#define right_ctrl  32 //定义右电机方向控制引脚gpio32.
#define right_pwm  25  //定义右电机PWM控制引脚gpio25.

//超声波传感器
#define TRIG_PIN 5 // 定义超声波的信号输入在gpio5.
#define ECHO_PIN 18 //定义超声波的信号输出在gpio18.
long distance; //定义距离变量

//舵机
int channel_PWM = 3; // 舵机通道
// 舵机频率，那么周期也就是1/50，也就是20ms ，PWM一共有16个通道，0-7位高速通道由80Mhz时钟驱动，后面8个为低速通道由1Mhz时钟驱动.
int freq_PWM = 50;
// PWM分辨率，取值为 0-20 之间  ，这里填写为10，那么后面的ledcWrite 这个里面填写的pwm值就在0-2的10次方之间，也就是0-1024，如果是要求不高的东西你可以直接拿1000去算了.
int resolution_PWM = 10;
// 绑定的IO，在下面的绑定函数里面会用到，绑定之后这个IO就会变成我们PWM的输出口.
const int servopin = 4;//定义舵机的IO脚位在gpio4.

void setup() {
  Serial.begin(115200); //设置波特率为115200.
  pinMode(left_ctrl,OUTPUT);//设置左电机方向控制引脚为输出.
  ledcSetup(0, 1200, 8);//设置LEDC通道0频率为1200，PWM分辨率为8，即占空比256.
  ledcAttachPin(26, 0);  //将LEDC通道0绑定到指定的左电机引脚gpio26上以实现输出.
  pinMode(right_ctrl,OUTPUT);//设置右电机方向控制引脚为输出.
  ledcSetup(1, 1200, 8);//设置LEDC通道1频率为1200，PWM分辨率为8，即占空比256.
  ledcAttachPin(25, 1);  //将LEDC通道1绑定到指定的右电机引脚gpio25上以实现输出.
  
  pinMode(TRIG_PIN,OUTPUT);//设置TRIG_PIN为输出.
  pinMode(ECHO_PIN,INPUT);//设置ECHO_PIN为输入.
  
  ledcSetup(3, 50, 10); // 设置舵机通道3频率为50,PWM分辨率为10.
  ledcAttachPin(4, 3);  //将 LEDC 通道绑定到指定 IO 口上以实现输出.
  ledcWrite(channel_PWM, 77);  //20ms高电平为1.5ms左右 ，也就是1.5/20*1024，设置舵机初始角度为90°.
  delay(300);
}

void loop() {
  distance = checkdistance();//获取超声波测量的距离.
  Serial.print(distance);//发送脉冲，以厘米计算距离并打印结果.
  Serial.println("cm");
  if(distance<8)//如果距离小于8
  {
    back();//后退
  }
  else if((distance>=8)&&(distance<13))//如果距离大于等于8，小于13.
  {
    Stop();//停止
  }
  else if((distance>=13)&&(distance<35))//如果距离大于等于13，小于35.
  {
    front();//跟随
  }
  else//如果以上都不是
  {
    Stop();//停止
  }
}

float checkdistance() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);
  float distance = pulseIn(ECHO_PIN, HIGH) / 58.00;
  delay(10);
  return distance;
}
   
void front()//定义前进的状态
{
  digitalWrite(left_ctrl,LOW); //左电机方向控制引脚低电平.
  ledcWrite(0, 200); //LEDC通道0绑定到指定的左电机输出PWM值为200.
  digitalWrite(right_ctrl,LOW); //右电机方向控制引脚低电平.
  ledcWrite(1, 200); //LEDC通道1绑定到指定的右电机输出PWM值为200.
}
void back()//定义后退的状态
{
  digitalWrite(left_ctrl,HIGH); //左电机方向控制引脚高电平.
  ledcWrite(0, 100); //LEDC通道0绑定到指定的左电机输出PWM值为100.
  digitalWrite(right_ctrl,HIGH); //右电机方向控制引脚高电平.
  ledcWrite(1, 100); //LEDC通道1绑定到指定的右电机输出PWM值为100.
}
void Stop()//定义停止的状态
{
  digitalWrite(left_ctrl,LOW);//左电机方向控制引脚低电平.
  ledcWrite(0, 0); //LEDC通道0绑定到指定的左电机输出PWM值为0.
  digitalWrite(right_ctrl,LOW);//右电机方向控制引脚低电平.
  ledcWrite(1, 0); //LEDC通道1绑定到指定的右电机输出PWM值为0.
}
//*************************************************************************************
```
## 5.实验现象：
编译并上传代码到ESP32主板（<span style="color: rgb(255, 76, 65);">如果上传代码不成功，可以在点击![Img](/media/img-20230330092521.png)后用手按住ESP32主板上的Boot键，出现上传进度百分比再松开Boot键![Img](/media/img-20230331144331.png)</span>）,小车安上电池，并且将电源开关拨到ON端，上电后，小车可以随着前方障碍物的移动而移动（只能在一条线直上，不能转弯）。