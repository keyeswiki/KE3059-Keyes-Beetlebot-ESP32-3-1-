# 项目04: 舵机转动

## 1.实验简介：
小车上有两个舵机，这里以接在引脚GPIO4的舵机为例，舵机是一种可以非常精确地旋转的电机。目前已广泛应用于玩具车、遥控直升机、飞机、机器人等领域。在这个项目中，我们将使用ESP32主板控制舵机转动。
## 2.元件知识：
![Img](/media/img-20230330095339.png)
**舵机：** 舵机是一种位置伺服的驱动器，主要是由外壳、电路板、无核心马达、齿轮与位置检测器所构成。其工作原理是由接收机或者单片机发出信号给舵机，其内部有一个基准电路，产生周期为20ms，宽度为1.5ms 的基准信号，将获得的直流偏置电压与电位器的电压比较，获得电压差输出。经由电路板上的IC 判断转动方向，再驱动无核心马达开始转动，透过减速齿轮将动力传至摆臂，同时由位置检测器送回信号，判断是否已经到达定位。适用于那些需要角度不断变化并可以保持的控制系统。当电机转速一定时，通过级联减速齿轮带动电位器旋转，使得电压差为0，电机停止转动。
舵机有多种规格，但它们都有三根连接线，分别是棕色、红色、橙色(不同品牌可能有不同的颜色)。棕色为GND，红色为电源正极，橙色为信号线
![Img](/media/img-20230324190535.png)
舵机的伺服系统由可变宽度的脉冲来进行控制，橙色的控制线是用来传送脉冲的。一般而言，PWM控制舵机的基准信号周期为20ms（50Hz），理论上脉宽应在1ms到2ms之间，对应控制舵机角度是0°～180°。但是，实际上更多控制舵机的脉宽范围是0.5ms 到2.5ms，具体需要自己实际调试下。
![Img](/media/img-20230324190601.png)
经过实测，舵机的脉冲范围为0.65ms~2.5ms。180度舵机，对应的控制关系是这样的：
|高电平时间|舵机角度|基准信号周期时间（20ms）|
| :--: | :--: | :--: |
|0.65ms|0度|0.65ms高电平+19.35ms低电平|
|1.5ms|90度|1.5ms高电平+18.5ms低电平|
|2.5ms|180度|2.5ms高电平+17.5ms低电平|

舵机的规格参数：
|工作电压：|DC 4.8V〜6V|
| :--: | :--: |
|可操作角度范围：|可操作角度范围：|
|脉波宽度范围：|500→2500 μsec|
|外观尺寸：|22.9*12.2*30mm|
|空载转速：|0.12±0.01 sec/60度（DC 4.8V）  0.1±0.01 sec/60度（DC 6V）|
|空载电流：|200±20mA（DC 4.8V）  220±20mA（DC 6V）|
|停止扭力：|1.3±0.01kg·cm（DC 4.8V）  1.5±0.1kg·cm（DC 6V）|
|停止电流：|≦850mA（DC 4.8V）  ≦1000mA（DC 6V）|
|待机电流：|3±1mA（DC 4.8V）  4±1mA（DC 6V）|
|重量:|9±1g (不带舵机轴)|
|使用温度：|-30℃~60℃|

## 3.实验接线：
| 舵机 | 小车PCB板 |
| :--: | :--: |
| 棕线 | G |
| 红线 | 5V |
| 橙黄线 | S1（GPIO4） |
![Img](/media/img-20230508091835.png)

## 4.添加库文件：
首先我们先确保安装了库文件，不然代码会编译出错，这里我们用到库文件“<span style="color: rgb(0, 209, 0);">ESP32Servo</span>”，如何安装库文件请参考“<span style="color: rgb(255, 76, 65);">开发环境配置</span>”文件。如果库文件已安装，就跳过这一步骤。
![Img](/media/img-20230518101152.png)

## 5.实验代码1：
控制超声波传感器转动的舵机是由ESP32主板的 GPIO4 控制。

```
//*************************************************************************************
/*
项目 04.1 舵机转动
舵机塑料臂将以0°、45°、90°、135°、180°的角度反复旋转
*/
#include <Arduino.h> 

// 舵机通道 
int channel_PWM = 3;  
// 舵机频率，那么周期也就是1/50，也就是20ms ，PWM一共有16个通道，0-7位高速通道由80Mhz时钟驱动，后面8个为低速通道由1Mhz时钟驱动.
int freq_PWM = 50;   
// PWM分辨率，取值为 0-20 之间  ，这里填写为10，那么后面的ledcWrite这个里面填写的pwm值就在0 - 2的10次方 之间 也就是 0-1024 ，如果是要求不高的东西你可以直接拿1000去算了.
int resolution_PWM = 10;   
// 绑定的IO，在下面的绑定函数里面会用到，绑定之后这个IO就会变成我们PWM的输出口.
const int PWM_Pin = 4;  //指定pwm绑定到GPIO4输出.

void setup() {
  Serial.begin(115200); //设置波特率为115200.
  ledcSetup(channel_PWM, freq_PWM, resolution_PWM); // 设置舵机通道,舵机频率,PWM分辨率.
  ledcAttachPin(PWM_Pin, channel_PWM);  //将LEDC通道绑定到指定 IO 口上以实现输出
}

void get_pwm_info()
{
  Serial.println("*******************************************************************");
  Serial.print("Reads the value of the specified channel duty cycle：");
  Serial.println(ledcRead(channel_PWM));  //读取指定通道占空比的值
  Serial.print("Reads the value of the specified channel frequency as：");
  Serial.println(ledcReadFreq(channel_PWM));  //返回指定通道当前频率（如果当前占空比为0 则该方法返回0).
}

void loop() {
  ledcWrite(channel_PWM, 25);  //20ms高电平为0.5ms左右 ，也就是0.5/20*1024，此时舵机角度为0°.
  get_pwm_info();  //打印信息，点击IDE右上角的串口查看器就可以看到打印的内容了
  delay(1000);
  ledcWrite(channel_PWM, 52);  //20ms高电平为1ms左右 ，也就是1/20*1024，此时舵机角度为45°.
  get_pwm_info();  //打印信息，点击IDE右上角的串口查看器就可以看到打印的内容了
  delay(1000);
  ledcWrite(channel_PWM, 77);  //20ms高电平为1.5ms左右 ，也就是1.5/20*1024，此时舵机角度为90°.
  get_pwm_info();
  delay(1000);
  ledcWrite(channel_PWM, 102);  //20ms高电平为2ms左右 ，也就是2/20*1024，此时舵机角度为135°.
  get_pwm_info();
  delay(1000);
  ledcWrite(channel_PWM, 128);  //20ms高电平为2.5ms左右 ，也就是2.5/20*1024，此时舵机角度为180°.
  get_pwm_info();
  delay(1000);
  //ledcDetachPin(PWM_Pin);  //这个是解除IO口的pwm输出功能模式.
}
//*************************************************************************************
```
## 6.实验现象1：
编译并上传代码到ESP32主板（<span style="color: rgb(255, 76, 65);">如果上传代码不成功，可以在点击![Img](/media/img-20230330092521.png)后用手按住ESP32主板上的Boot键，出现上传进度百分比再松开Boot键![Img](/media/img-20230331144331.png)</span>）， 利用USB线上电后，再打开串口监视器![Img](/media/img-20230330103640.png)，设置波特率为115200，串口监视器窗口打印指定通道占空比的值和指定通道当前频率值，同时舵机塑料臂将以0°、45°、90°、135°、180°的角度转动。循环进行！！

![Img](/media/img-20230330103315.png)

其实我们还可以有一种更简单的方法控制舵机，就是使用Arduino ESP32的舵机库文件，可以参考Arduino 官方的使用说明：https://www.arduino.cc/en/Reference/Servo 。
以下是使用了舵机库文件的程序代码。

## 7.实验代码2：

```
//*************************************************************************************
/*
项目 04.2 舵机转动
舵机将从0度旋转到180度，然后反转使它从180度旋转到0度的方向不断地重复这些动作.
*/
#include <ESP32Servo.h>

Servo myservo;  // 创建舵机对象来控制舵机

int posVal = 0;    // 创建变量来存储舵机位置
int servoPin = 4; // 舵机引脚

void setup() {
  myservo.setPeriodHertz(50);           // 标准50赫兹舵机
  myservo.attach(servoPin, 500, 2500);  // 将servoPin上的舵机附加到舵机对象上
}
void loop() {

  for (posVal = 0; posVal <= 180; posVal += 1) { // 从0°到180°
    // 以1度为阶梯
    myservo.write(posVal);       // 告诉舵机进入可变'pos'位置
    delay(15);                   // 等待15ms舵机到达位置
  }
  for (posVal = 180; posVal >= 0; posVal -= 1) { // 从180°到0°
    myservo.write(posVal);       // 告诉舵机进入可变'pos'位置
    delay(15);                   // 等待15ms舵机到达位置
  }
}
//*************************************************************************************
```

## 8.实验现象2：
编译并上传代码到ESP32主板上（<span style="color: rgb(255, 76, 65);">如果上传代码不成功，可以在点击![Img](/media/img-20230330092521.png)后用手按住ESP32主板上的Boot按钮，出现上传进度百分比再松开Boot按钮![Img](/media/img-20230331144331.png)，如下图所示：</span>），利用USB线上电后，舵机塑料臂将从0度旋转到180度，然后反转方向，使其从180度旋转到0度，循环往复地重复这些动作。




